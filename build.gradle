buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'bintray'
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    compile(
            fileTree(dir: 'lib', include: '*.jar')
    )
    testCompile (
            'junit:junit:4.10',
            'org.hamcrest:hamcrest-core:1.1',
            'commons-io:commons-io:1.3.2',
            'org.mockito:mockito-core:1.9.5',
            'org.objenesis:objenesis:1.0'
    )
}

buildDir = 'bin'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test-resources'
        }

    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName = jar.baseName
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName = jar.baseName
    classifier = 'javadoc'
    from javadoc.destinationDir
}
javadoc.failOnError = false

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.sbelei'
            artifactId = 'rally-api-helper'
            version = '1.1'

            from components.java
            artifact sourcesJar {
                classifier "source"
            }
        }
    }
}

bintray {
    user = 'crc83'
    key = bintrayApiKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'RallyApiHelper'
        licenses = ['LGPL']
    }
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
    logger.info "Installing $project.name"
}

